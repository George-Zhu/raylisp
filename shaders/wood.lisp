(in-package :raylisp)

(defclass wood-shader (shader transform-mixin)
  ((start :initarg :start :reader start-of)
   (end :initarg :end :reader end-of)))

(defmethod compute-shader-function ((shader wood-shader) object scene transform)
  (let* ((matrix (matrix* transform (transform-of shader)))
         (start (compile-shader (start-of shader) object scene matrix))
         (end (compile-shader (end-of shader) object scene matrix))
         (inverse (inverse-matrix (matrix* transform (transform-of object) (transform-of shader)))))
    (declare (optimize speed))
    (lambda (obj point normal n.d ray counters)
      (let* ((p (transform-point point inverse))
             (d (+ (sqrt (+ (square (aref p 0)) (square (aref p 1))))
                   (* 0.1 (perlin-noise p 3 0.9))))
             (start-color (funcall start obj point normal n.d ray counters))
             (end-color (funcall end obj point normal n.d ray counters)))
        (declare (single-float d))
        (%vec-lerp p end-color start-color (mod d 1.0))))))
