(in-package :raylisp)

(defclass marble-shader (shader transform-mixin)
  ((start :initarg :start :reader start-of)
   (end :initarg :end :reader end-of)))

(defmethod compute-shader-function ((shader marble-shader) object scene transform)
  (let ((start (compile-shader (start-of shader) object scene transform))
        (end (compile-shader (end-of shader) object scene transform))
        (inverse (inverse-matrix transform)))
    (declare (optimize speed))
    (lambda (obj point normal n.d ray counters)
      (let* ((p (transform-point point inverse))
             (noise (abs (cos (+ (aref p 0) (turbulence p 0.4 4.0)))))
             (start-color (funcall start obj point normal n.d ray counters))
             (end-color (funcall end obj point normal n.d ray counters)))
        (%vec-lerp p end-color start-color (clamp noise 0.0 1.0))))))
