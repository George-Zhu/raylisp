(in-package :raylisp)

(defclass orthogonal-camera (camera) ())

(defmethod normalize-camera ((camera orthogonal-camera) width height)
  (let ((ratio (coerce (/ width height) 'single-float))
        (new (call-next-method)))
    (setf (slot-value new 'right) (vec* (right-of new) ratio))
    new))

(defmethod compute-camera-function ((camera orthogonal-camera))
  (let ((dir (normalize (direction-of camera)))
	(right (right-of camera))
	(up (up-of camera))
	(location (location-of camera)))
    (declare (type vec dir right up location) (optimize speed))
    (lambda (fun rx ry counters)
      (declare (float rx ry)
               (function fun))
      (note-camera-ray counters)
      (macrolet ((loc (n)
                   `(+ (aref location ,n)
                       (* rx (aref right ,n)) (* ry (aref up ,n)))))
	(let ((loc (vec (loc 0) (loc 1) (loc 2))))
          (declare (dynamic-extent loc))
          (with-ray (ray :origin loc :direction dir)
            (funcall fun ray)))))))